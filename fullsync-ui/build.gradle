
apply plugin: 'java'

def addSWTDependency(configuration, osName, osArch) {
	def arch = osArch.contains('64') ? 'x86_64' : 'x86'
	def os = ''
	if (osName.contains('windows')) {
		os = 'win32.win32'
	}
	else if (osName.contains('linux')) {
		os = 'gtk.linux'
	}
	else if (osName.contains('mac')) {
		os = 'cocoa.macosx'
	}
	project.dependencies.add(configuration, "org.eclipse.platform:org.eclipse.swt.${os}.${arch}:${ext.swtVersion}") {
		exclude group: 'org.eclipse.platform', module: 'org.eclipse.swt'
	}
}

def addLocalSWTVersion(configuration) {
	return addSWTDependency(configuration, System.properties['os.name'].toLowerCase(), System.properties['os.arch'])
}

configurations {
	dist.extendsFrom runtime
}

dependencies {
	compile project(':fullsync-core')
	compile project(':fullsync-assets')
	dist project(':fullsync-ui')
}

addSWTDependency "dist", "linux", "x86"
addSWTDependency "dist", "linux", "x86_64"
addSWTDependency "dist", "mac", "x86_64"
addSWTDependency "dist", "windows", "x86"
addSWTDependency "dist", "windows", "x86_64"
addLocalSWTVersion "compile"

task run(type: JavaExec) {
	main = 'net.sourceforge.fullsync.ui.GuiMain'
	classpath = configurations.runtime + sourceSets.main.output
}
